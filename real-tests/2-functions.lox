fun assert(input, expected) {
    if (input != expected) {
        print "ERROR";
    } else {
        print "OK";
    }
}

print "--- SCOPE HANDLING ---";
var x = 0;
var y = 0;
assert(x, 0);
assert(y, 0);
{
    var x = 1;
    y = 1;
    assert(x, 1);
    assert(y, 1);
}
assert(x, 0);
assert(y, 1);

print "--- SIMPLE FUNCTION USAGE ---";
fun fib(n) {
    if (n <= 1) return n;
    return fib(n-2) + fib(n-1);
}

fun noreturn() { }

fun emptyreturn() {
    return;
}

assert(fib(20), 6765);
assert(noreturn(), nil);
assert(emptyreturn(), nil);

print "--- NESTED FUNCTIONS ---";
fun make_counter() {
    var i = 0;
    fun count() {
        i = i + 1;
        return i;
    }
    return count;
}

var x_counter = make_counter();
var y_counter = make_counter();
assert(x_counter(), 1);
assert(x_counter(), 2);
assert(x_counter(), 3);
assert(y_counter(), 1);
assert(y_counter(), 2);
assert(y_counter(), 3);
assert(x_counter(), 4);
assert(y_counter(), 4);

print "--- VARIABLE SHADOWING ---";
var a = 1;
assert(a, 1);
{
   var a = 2;
   assert(a, 2);
   {
    var a = 3;
    assert(a, 3);
   }
}


print "--- CLOSURE BUG ---";
var a = "global";
{
   fun ret_a() {
     return a;
   }

   assert(ret_a(), "global");
   var a = "block";
   assert(ret_a(), "global");
}
